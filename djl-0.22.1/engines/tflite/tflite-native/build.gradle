plugins {
    id 'maven-publish'
    id 'signing'
}

apply from: file("${rootProject.projectDir}/tools/gradle/cpp-formatter.gradle")

group = "ai.djl.tflite"
boolean isRelease = project.hasProperty("release") || project.hasProperty("staging")
version = "${tflite_version}" + (isRelease ? "" : "-SNAPSHOT")
def BINARY_ROOT = "${project.buildDir}/download"

// Create a placeholder jar without classifier to pass sonatype tests but throws an Exception if loaded
jar {
    def placeholder = "${project.buildDir}/placeholder"
    // this line is to enforce gradle to build the jar
    // otherwise it don't generate the placeholder jar at times
    // when there is no java code inside src/main
    outputs.dir file("build/libs")
    doFirst {
        def versionName = project.version
        if (!isRelease) {
            versionName += String.format("-%s", new Date().format('yyyyMMdd'))
        }
        def dir = file("${placeholder}/native/lib")
        dir.mkdirs()
        def propFile = file("${placeholder}/native/lib/tflite.properties")
        propFile.text = "placeholder=true\nversion=${versionName}\n"
    }

    from placeholder
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

project.tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

signing {
    required(project.hasProperty("staging") || project.hasProperty("snapshot"))
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}

publishing.repositories {
    maven {
        if (project.hasProperty("snapshot")) {
            name = "snapshot"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username = findProperty("ossrhUsername")
                password = findProperty("ossrhPassword")
            }
        } else if (project.hasProperty("staging")) {
            name = "staging"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = findProperty("ossrhUsername")
                password = findProperty("ossrhPassword")
            }
        } else {
            name = "local"
            url = "build/repo"
        }
    }
}

def downloadJni(String dir) {
    String url = "https://djl-ai.s3.amazonaws.com/publish/tflite/${tflite_version}/jnilib"
    def files = [
            "macOS/libtensorflowlite_jni.so": "cpu/osx-x86_64/native/lib/libtensorflowlite_jni.dylib",
            "Linux/libtensorflowlite_jni.so": "cpu/linux-x86_64/native/lib/libtensorflowlite_jni.so"
    ]

    for (Map.Entry<String, String> entry : files.entrySet()) {
        project.logger.lifecycle("Downloading ${url}/${entry.key}")
        def file = new File("${dir}/${entry.value}")
        file.getParentFile().mkdirs()
        new URL("${url}/${entry.key}").withInputStream { i -> file.withOutputStream { it << i } }
    }
}

tasks.register('downloadTfliteNativeLib') {
    doLast {
        delete "${BINARY_ROOT}"
        downloadJni("${BINARY_ROOT}")
    }
}

tasks.register('uploadS3') {
    doLast {
        delete "${BINARY_ROOT}"
        downloadJni("${BINARY_ROOT}")
        exec {
            commandLine "sh", "-c", "find ${BINARY_ROOT} -type f | xargs gzip"
        }
        def f = new File("${BINARY_ROOT}/files.txt")
        def uploadDirs = [
                "${BINARY_ROOT}/cpu/linux-x86_64/native/lib/",
                "${BINARY_ROOT}/cpu/osx-x86_64/native/lib/"
        ]
        uploadDirs.each { item ->
            fileTree(item).files.name.each {
                def out = item - "${BINARY_ROOT}/"
                f << out + URLEncoder.encode(it, "UTF-8")
                f << "\n"
            }
        }
        exec {
            commandLine "aws", "s3", "sync", "${BINARY_ROOT}", "s3://djl-ai/publish/tflite/${tflite_version}/"
        }
    }
}


def flavorNames = file(BINARY_ROOT).list() ?: []
flavorNames.each { flavor ->

    def platformNames = file("${BINARY_ROOT}/${flavor}").list() ?: []

    def artifactsNames = []

    platformNames.each { osName ->
        tasks.create(name: "${flavor}-${osName}Jar", type: Jar) {
            doFirst {
                def propFile = file("${BINARY_ROOT}/${flavor}/${osName}/native/lib/tflite.properties")
                propFile.delete()
                def dsStore = file("${BINARY_ROOT}/${flavor}/${osName}/native/lib/.DS_Store")
                dsStore.delete()

                def versionName = String.format("${project.version}-%s", new Date().format('yyyyMMdd'))
                def dir = file("${BINARY_ROOT}/${flavor}/${osName}/native/lib")
                def sb = new StringBuilder()
                sb.append("version=${versionName}\nclassifier=${flavor}-${osName}-x86_64\nlibraries=")
                def first = true
                for (String name : dir.list().sort()) {
                    if (first) {
                        first = false
                    } else {
                        sb.append(',')
                    }
                    sb.append(name)
                }
                propFile.text = sb.toString()
            }
            from file("${BINARY_ROOT}/${flavor}/${osName}")
            from file("src/main/resources")
            from file("${project.buildDir}/classes/java/main")
            archiveClassifier = "${osName}"

            manifest {
                attributes("Automatic-Module-Name": "ai.djl.tflite_native_${flavor}_${osName}")
            }
        }
        artifactsNames.add(tasks["${flavor}-${osName}Jar"])
    }

    // Only publish if the project directory equals the current directory
    // This means that publishing from the main project does not publish the native jars
    // and the native jars have to be published separately
    if (project.getProjectDir().toString() == System.getProperty("user.dir")) {
        publishing.publications.create("${flavor}", MavenPublication) {
            artifactId "tflite-native-${flavor}"
            from components.java
            artifacts = artifactsNames
            artifact jar
            artifact javadocJar
            artifact sourcesJar
            pom {
                name = "DJL release for TFLite native binaries"
                description = "Deep Java Library (DJL) provided TFLite native library binary distribution"
                url = "http://www.djl.ai/engines/tflite/tflite-native"
                packaging = "jar"

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                scm {
                    connection = "scm:git:git@github.com:deepjavalibrary/djl.git"
                    developerConnection = "scm:git:git@github.com:deepjavalibrary/djl.git"
                    url = "https://github.com/deepjavalibrary/djl"
                    tag = "HEAD"
                }

                developers {
                    developer {
                        name = "DJL.AI Team"
                        email = "djl-dev@amazon.com"
                        organization = "Amazon AI"
                        organizationUrl = "https://amazon.com"
                    }
                }
            }
        }
    }
}
