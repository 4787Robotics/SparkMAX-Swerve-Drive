configure([
        project(':api'),
        project(':basicdataset'),
        project(':djl-zero'),
        project(':engines:ml:xgboost'),
        project(':engines:ml:lightgbm'),
        project(':engines:mxnet:mxnet-engine'),
        project(':engines:mxnet:mxnet-model-zoo'),
        project(':engines:onnxruntime:onnxruntime-android'),
        project(':engines:onnxruntime:onnxruntime-engine'),
        project(':engines:paddlepaddle:paddlepaddle-engine'),
        project(':engines:paddlepaddle:paddlepaddle-model-zoo'),
        project(':engines:pytorch:pytorch-engine'),
        project(':engines:pytorch:pytorch-jni'),
        project(':engines:pytorch:pytorch-model-zoo'),
        project(':engines:tensorflow:tensorflow-api'),
        project(':engines:tensorflow:tensorflow-engine'),
        project(':engines:tensorflow:tensorflow-model-zoo'),
        project(':engines:tensorrt'),
        project(':engines:tflite:tflite-engine'),
        project(':extensions:audio'),
        project(':extensions:aws-ai'),
        project(':extensions:fasttext'),
        project(':extensions:hadoop'),
        project(':extensions:opencv'),
        project(':extensions:sentencepiece'),
        project(':extensions:tablesaw'),
        project(':extensions:timeseries'),
        project(':extensions:tokenizers'),
        project(':extensions:spark'),
        project(':model-zoo')]) {
    apply plugin: "maven-publish"
    apply plugin: "signing"

    project.tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        title "Deep Java Library ${version} - ${project.name} API"
        options.encoding = "UTF-8"
        options.overview "src/main/javadoc/overview.html"
        options.addBooleanOption("-allow-script-in-comments", true)
        options.header "<script type='text/javascript' src='https://cdnjs.cloudflare.com/ajax/libs/mathjax/2.7.1/MathJax.js?config=TeX-AMS-MML_HTMLorMML'></script>"
    }

    signing {
        required(project.hasProperty("staging") || project.hasProperty("snapshot"))
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                artifacts = [jar, javadocJar, sourcesJar]
                pom {
                    name = "Deep Java Library - ${project.name}"
                    description = "Deep Java Library - ${project.name}"
                    url = "http://www.djl.ai/${project.name}"

                    packaging = "jar"

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }

                    scm {
                        connection = "scm:git:git@github.com:deepjavalibrary/djl.git"
                        developerConnection = "scm:git:git@github.com:deepjavalibrary/djl.git"
                        url = "https://github.com/deepjavalibrary/djl"
                        tag = "HEAD"
                    }

                    developers {
                        developer {
                            name = "DJL.AI Team"
                            email = "djl-dev@amazon.com"
                            organization = "Amazon AI"
                            organizationUrl = "https://amazon.com"
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                if (project.hasProperty("snapshot")) {
                    name = "snapshot"
                    url = "https://oss.sonatype.org/content/repositories/snapshots/"
                    credentials {
                        username = findProperty("ossrhUsername")
                        password = findProperty("ossrhPassword")
                    }
                } else if (project.hasProperty("staging")) {
                    name = "staging"
                    url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                    credentials {
                        username = findProperty("ossrhUsername")
                        password = findProperty("ossrhPassword")
                    }
                } else {
                    name = "local"
                    url = "build/repo"
                }
            }
        }
    }
}
